//===== rAthena Script =======================================
//= Courtesy Ticket Exchanger
//===== By: ==================================================
//= Poutine
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Custom Exchanger to exchange Courtesy Tickets for EXP and
//= goods for RedditRO
//===== Additional Comments: =================================
//= 1.0 First Version. [Poutine]
//============================================================

moc_para01,49,170,3	script	Ticket Exchanger	10039,{
	if (countitem(6219) < 1) {
		mes "Wait a minute!";
		mes "-You need to have an-";
		mes "-^4d4dff'Eden Group Mark'^000000-";
		mes "-to receive these missions.-";
		next;
		switch(select("Just walk away.:^0000FFEden Group^000000 missions?:How can I be a member of the ^0000FFEden Group^000000?")) {
		case 1:
			close;
		case 2:
			mes "::The Eden Group Missions";
			mes "::are basically missions";
			mes "::requested by";
			mes "::people from all around the kingdom.";
			mes "::We just categorize them.";
			next;
			mes "::In the Eden Group,";
			mes "::we sorted out the missions";
			mes "::according to the level of difficulty.";
			mes "::According to this level,";
			mes "::you can find";
			mes "::missions that";
			mes "::suit you.";
			next;
			mes "::When you are done,";
			mes "::you can come back";
			mes "::to the mission board.";
			next;
			mes "::Then you will be";
			mes "::rewarded!!";
			mes "::Simple, isn't it!!";
			mes "::No commission required!!";
			close;
		case 3:
			mes "::You can become a member";
			mes "::of the Eden Group";
			mes "::to ^0000FFLaime Evenor^000000,";
			mes "::who is waiting on the 1st floor.";
			next;
			mes "::If you become a member,";
			mes "::you will get an";
			mes "::\"^006400Eden Group Mark^000000\".";
			mes "::Then you will be able";
			mes "::to see the mission board.";
			close;
		}
	}
	
	mes "[Ticket Exchanger]";
	if (BaseLevel < 100) {
		mes "I'm sorry, I cannot be of service to you.";
		close;
	}
	mes "Welcome " + strcharinfo(0) + "!";
	mes "I can exchange your ^4d4dff'Courtesy Tickets'^000000 for certain things!";
	mes "What would you like?";
	next;
	switch(select("Items:Experience:Cancel")) {
		case 1: // Items TODO: Figure out appropriate items
			mes "[Ticket Exchanger]";
			mes "Not implemented yet :(";
			close;
		case 2: // Experience
			if(BaseLevel > 170) {
				set .@lvlRange, 1;
			} else if(BaseLevel > 160) {
				set .@lvlRange, 2;
			} else if(BaseLevel > 150) {
				set .@lvlRange, 3;
			} else if(BaseLevel > 140) {
				set .@lvlRange, 4;
			} else if(BaseLevel > 130) {
				set .@lvlRange, 5;
			} else if(BaseLevel > 120) {
				set .@lvlRange, 6;
			} else if(BaseLevel > 110) {
				set .@lvlRange, 7;
			} else if(BaseLevel >= 100) {
				set .@lvlRange, 8;
			}
			callsub L_TickExp, .@lvlRange;
		case 3: // Cancel
			close;
	}
	end;
	
L_TickExp:
	set .@lvRange, getarg(0);
	mes "[Ticket Exchanger]";
	mes "Hmm...let's see.";
	
	switch(.@lvRange) {
		case 1: set .@numTickets, 100;	break;
		case 2: set .@numTickets, 90;	break;
		case 3: set .@numTickets, 80;	break;
		case 4: set .@numTickets, 70;	break;
		case 5: set .@numTickets, 60;	break;
		case 6: set .@numTickets, 50;	break;
		case 7: set .@numTickets, 40;	break;
		case 8: set .@numTickets, 25;	break;
	}
	
	mes "It will cost you " + .@numTickets + " to exchange for some experience.";
	next;
	switch(select("Okay:Nevermind")) {
		case 1:
			if(countitem(6549) < .@numTickets) {
				mes "[Ticket Exchanger]";
				mes "You don't seem to have enough ^4d4dff'Courtesy Tickets'^000000...";
				close;
			}
			switch(.@lvRange) {
				case 1:
					set .@bxp, 3000000 + rand(0,3) * 100000;
					set .@jxp, 450000 + rand(0,3) * 10000;
					break;
				case 2:
					set .@bxp, 2500000 + rand(0,3) * 100000;
					set .@jxp, 400000 + rand(0,3) * 10000;
					break;
				case 3:
					set .@bxp, 2250000 + rand(0,3) * 100000;
					set .@jxp, 350000 + rand(0,3) * 10000;
					break;
				case 4:
					set .@bxp, 2000000 + rand(0,3) * 100000;
					set .@jxp, 300000 + rand(0,3) * 10000;
					break;
				case 5:
					set .@bxp, 1500000 + rand(0,3) * 100000;
					set .@jxp, 230000 + rand(0,3) * 10000;
					break;
				case 6:
					set .@bxp, 800000 + rand(0,3) * 100000;
					set .@jxp, 180000 + rand(0,3) * 10000;
					break;
				case 7:
					set .@bxp, 300000 + rand(0,3) * 100000;
					set .@jxp, 160000 + rand(0,3) * 10000;
					break;
				case 8:
					set .@bxp, 200000 + rand(0,3) * 50000;
					set .@jxp, 150000 + rand(0,3) * 10000;
					break;
			}		
			delitem 6549, .@numTickets;
			getexp .@bxp, .@jxp;
			mes "[Ticket Exchanger]";
			mes "Enjoy!";
		case 2:
			close;
	}
	close;
}	